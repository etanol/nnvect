sans = "/usr/share/fonts/truetype/ttf-liberation/LiberationSans-Regular.ttf"
set terminal png font sans 12 size 1000,500

set style data histograms
set style histogram clustered gap 2
set style fill solid 0.2
set boxwidth 0.8 relative
set key vertical top rmargin Left reverse noautotitle

set autoscale
set yrange [0:]
set xtics nomirror out rotate by -45
set ytics mirror
set ylabel "MOPS"
set grid y

set title plot_title." (I)"
set output plot_output."-1.png"

plot plot_data u 2:xtic(1) t "Scalar simple", '' u 3 t "Scalar unroll 2", \
         '' u 4 t "Scalar unroll 4", '' u 5 t "Vectorized simple", \
         '' u 6 t "Vectorized unroll 2", '' u 7 t "Vectorized unroll 4"

set title plot_title." (II)"
set output plot_output."-2.png"

plot newhistogram "Simple", \
         plot_data u 2:xtic(1) t "Scalar", '' u 5 t "Vectorized", \
     newhistogram "Unroll 2", \
         plot_data u 3:xtic(1) lt 1, '' u 6 lt 2, \
     newhistogram "Unroll 4", \
         plot_data u 4:xtic(1) lt 1, '' u 7 lt 2

set title plot_title." (III)"
set output plot_output."-3.png"

plot newhistogram "Scalar", \
         plot_data u 2:xtic(1) t "Simple", '' u 3 t "Unroll 2", \
         '' u 4 t "Unroll 4", \
     newhistogram "Vectorized", \
         plot_data u 5:xtic(1) lt 1, '' u 6 lt 2, '' u 7 lt 3

