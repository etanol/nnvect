sans = "/usr/share/fonts/truetype/ttf-liberation/LiberationSans-Regular.ttf"
set terminal png font sans 12 size 1000,500

set style data histograms
set style histogram clustered gap 2
set style fill solid 0.2
set boxwidth 0.8 relative
set key vertical top rmargin Left reverse noautotitle

set autoscale
set yrange [0:]
set xtics nomirror out rotate by -45
set ytics mirror
set ylabel "Time (seconds)"
set grid y

set output plot_output."-b.png"
set title plot_title." 2-level blocking"
plot newhistogram "Scalar", \
         plot_data u 4:xtic(1) t "Simple", '' u 7 t "Unroll 2", \
         '' u 10 t "Unroll 4", \
     newhistogram "Vectorized", \
         plot_data u 13:xtic(1) lt 1, '' u 16 lt 2, '' u 19 lt 3

set output plot_output."-s.png"
set title plot_title." scalar"
plot newhistogram "Blocking 0", \
         plot_data u 2:xtic(1) t "Simple", '' u 5 t "Unroll 2", \
         '' u 8 t "Unroll 4", \
     newhistogram "Blocking 1", \
         plot_data u 3:xtic(1) lt 1, '' u 6 lt 2, '' u 9 lt 3, \
     newhistogram "Blocking 2", \
         plot_data u 4:xtic(1) lt 1, '' u 7 lt 2, '' u 10 lt 3

set output plot_output."-v.png"
set title plot_title." vectorized"
plot newhistogram "Blocking 0", \
         plot_data u 11:xtic(1) t "Simple", '' u 14 t "Unroll 2", \
         '' u 17 t "Unroll 4", \
     newhistogram "Blocking 1", \
         plot_data u 12:xtic(1) lt 1, '' u 15 lt 2, '' u 18 lt 3, \
     newhistogram "Blocking 2", \
         plot_data u 13:xtic(1) lt 1, '' u 16 lt 2, '' u 19 lt 3

